#
#   Copyright (C) 2002-2005, International Business Machines Corporation and others.
#       All Rights Reserved.
#
#   file:  sent.txt
#
#   ICU Sentence Break Rules
#      See Unicode Standard Annex #29.
#      These rules are based on TR 29 version 4.1.0
#


#
# Character categories as defined in TR 29
#
$Sep       = [\p{Sentence_Break = Sep}];
$Format    = [\p{Sentence_Break = Format}];
$Sp        = [\p{Sentence_Break = Sp}];
$Lower     = [\p{Sentence_Break = Lower}];
$Upper     = [\p{Sentence_Break = Upper}];
$OLetter   = [\p{Sentence_Break = OLetter}];
$Numeric   = [\p{Sentence_Break = Numeric}];
$ATerm     = [\p{Sentence_Break = ATerm}];
$STerm     = [\p{Sentence_Break = STerm}];
$Close     = [\p{Sentence_Break = Close}];

#
# Define extended forms of the character classes,
#   incorporate grapheme cluster + format chars.
#   Rules 4 and 5.  

$CR         = \u000d;
$LF         = \u000a;
$Extend     = [[:Grapheme_Extend = TRUE:]];
$Control    = [\p{Grapheme_Cluster_Break = Control}];

$SpEx       = $Sp      $Extend* $Format*;
$LowerEx    = $Lower   $Extend* $Format*;
$UpperEx    = $Upper   $Extend* $Format*;
$OLetterEx  = $OLetter $Extend* $Format*;
$NumericEx  = $Numeric $Extend* $Format*;
$ATermEx    = $ATerm   $Extend* $Format*;
$STermEx    = $STerm   $Extend* $Format*;
$CloseEx    = $Close   $Extend* $Format*;


## -------------------------------------------------

!!chain;

!!forward;

# Rule 3 - break after separators.
#
$CR $LF {100};    # break status {100} is UBRK_SENTENCE_SEP
$Sep / {100};


# Rule 4 - don't break grapheme clusters, including optional trailing format chars.
#          
[^$Control $Sep] $Extend+ $Format*;
[^$Control $Sep] $Extend* $Format+;


# Rule 6
$ATermEx $NumericEx;

# Rule 7
$UpperEx $ATermEx $UpperEx;

#Rule 8
$NotLettersEx = ([^$OLetter $Upper $Lower $Sep $Control] $Extend* $Format*) | 
                ([^$OLetter $Upper $Lower $Sep] $Format*);
$ATermEx $CloseEx* $SpEx* $NotLettersEx* $Lower;

#Rule 9, 10, 11
($STermEx | $ATermEx) $CloseEx* $SpEx* $Sep?;

#Rule 12
[[^$STerm $ATerm $Close $Sp $Sep $Control $Extend]{bof}] $Extend* $Format* $CloseEx* $SpEx* .;

## -------------------------------------------------

!!reverse;
# TODO
.*;

## -------------------------------------------------

#!!safe_reverse;

# rule 4
#$Extend+ [^$Extend];

# rule 7
#$Extend* $ATerm $Format* $Extend* $Upper;

# rule 8
#($Extend* $Term)+ ($Extend* $Sp $Format*)* ($Extend* $Close $Format*)* $Extend* $ATerm;

# rule 11
#($Extend* $Sp $Format*)* ($Extend* $Close $Format*)*;
#($Extend* $Sp $Format*)* ($Extend* $Close $Format*)* $Extend* ($Term | $ATerm);

## -------------------------------------------------

!!safe_forward;

# rule 7

#$ATerm $Extend* $Format* $Upper;

# rule 8

#$Lower .;

# rule 11

#($Close $Extend* $Format*)* ($Sp $Extend* $Format*)*;